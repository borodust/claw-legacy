(uiop:define-package :claw.util
  (:use :cl :alexandria)
  (:import-from :claw-utils
                #:common-prefix)
  (:export #:+known-platforms+
           #:+byte-size+

           #:with-evaluated-variables
           #:with-evaluated-lists
           #:find-path
           #:list-all-known-include-paths
           #:list-all-known-framework-paths
           #:default-c-name-to-lisp

           #:get-timestamp
           #:common-prefix

           #:parse-renaming-pipeline
           #:with-symbol-renaming
           #:c-name->lisp
           #:string+
           #:parse-infix

           #:with-local-cpu
           #:with-local-environment
           #:with-windows-environment
           #:local-environment
           #:local-platform

           #:with-temporary-directory

           #:remove-template-argument-string
           #:extract-template-argument-string
           #:split-template-argument-string-into-literals
           #:reformat-template-argument-string
           #:split-template-name-into-groups
           #:join-groups-into-template-name

           #:ignore-functions))
(uiop:define-package :claw.util.infix
  (:use))
(cl:in-package :claw.util)


(declaim (special *symbol-package*
                  *symbol-type*
                  *symbol-renaming-pipeline*
                  *hit-count*))


(define-constant +byte-size+ 8)

(define-constant +path-search-regex+
  "\\> search starts here:\\s*((.|\\s)*)?\\s*End of search list"
  :test #'equal)

(define-constant +stupid-darwin-framework-postfix+
  " (framework directory)"
  :test #'equal)


(define-constant +known-platforms+
    '("i686-pc-linux-gnu"
      "x86_64-pc-linux-gnu"
      "i686-pc-windows-msvc"
      "x86_64-pc-windows-msvc"
      "i686-pc-windows-gnu"
      "x86_64-pc-windows-gnu"
      "i686-apple-darwin9"
      "x86_64-apple-darwin9"
      "i686-apple-darwin-gnu"
      "x86_64-apple-darwin-gnu"
      "i386-unknown-freebsd"
      "x86_64-unknown-freebsd"
      "i386-unknown-openbsd"
      "x86_64-unknown-openbsd"
      "arm-pc-linux-gnu")
  :test #'equal)


;;;
;;; TEMPLATE ARGUMENT/PARAMETER MANIPULATION
;;;
(defparameter *function-parameter-list-extractor* (ppcre:create-scanner "\\(.*\\("))

(defun remove-parameter-list-string (name)
  (ppcre:regex-replace-all *function-parameter-list-extractor* name ""))


(defun substring-trim (name start-idx end-idx)
  (string-trim '(#\Space #\Tab #\Newline) (subseq name start-idx end-idx)))


(defun split-template-name-into-groups (name)
  (labels ((%weird-char-p (idx)
             (and (< idx (length name))
                  (>= idx 0)
                  (char= (aref name idx) #\=)))
           (%extract (pos)
             (loop with result = nil
                   with len = (length name)
                   with last-end = pos
                   for idx from pos
                   when (= idx len)
                     do (unless (= last-end len)
                          (push (substring-trim name last-end len) result))
                        (return (values (nreverse result) len))
                   when (char= (aref name idx) #\,)
                     do (unless (= last-end idx)
                          (push (substring-trim name last-end idx) result)
                          (setf last-end (1+ idx)))
                   when (and (char= (aref name idx) #\>)
                             (not (%weird-char-p (1+ idx))))
                     do (unless (= last-end idx)
                          (push (substring-trim name last-end idx) result))
                        (return (values (nreverse result) (1+ idx)))
                   when (and (char= (aref name idx) #\<)
                             (not (%weird-char-p (1+ idx))))
                     do (when (> (- idx pos) 1)
                          (push (substring-trim name pos idx) result))
                        (multiple-value-bind (groups end)
                            (%extract (1+ idx))
                          (push groups result)
                          (setf idx (1- end)
                                last-end end)))))
    (%extract 0)))


(defun join-groups-into-template-name (groups)
  (format nil "窿祜镳骘ㄧ蝻躔铄舂镱珧秕痼泔祆邈ㄣ镱è扉篝珧秕皓ㄦ矧磲铋⒓辆觑轭珧秕痼轭麸翦眇灬翦钺礤珧秕皓┅è扉篝铄舂珧秕皓ㄦ矧磲铋连珧秕皓┅┅ㄤ彐躅蝈盹鲥翦眇灬翦狎珲礤铘篝蜷铉钺礤戾舄è珧秕痼箴扉舡翦眇灬翦钺礤轭麸珧秕痼钺礤┅灬篝珧秕ㄦ轵篝灬篝珧秕痼┅┅觑轭珧秕痼轭麸翦眇灬翦钺礤ㄩ扉篝灬篝珧秕皓ㄢ豸灬篝珧秕痼珧秕痼┅┅ㄤ彐躅屮趄徙舡翦眇灬翦狎珲礤铘篝蜷铉钺礤戾è珧秕ㄦ轵篝灬篝箴扉舡翦眇灬翦钺礤轭麸珧秕痼钺礤┅┅麒孱扉篝珧秕皓觑轭珧秕痼轭麸翦眇灬翦钺礤扉篝珧秕皓┅┅ㄤ彐躅箴扉舡翦眇灬翦狎珲礤铘篝蜷铉轭麸扉翦蜥祗钺礤戾è钺礤篚怏羼钺礤ū戾铉翳钺礤┅┅ㄦ戾è铄舡殇ㄣ躜蝈铘殇戾è汨狎ㄡ蝈钺礤沲蝌孱舡殇┅ㄦ戾è痫ㄣ祜箝铉汨狎祜镳鏖翳溴痿骘殇骝镯ū沲蝌孱舡殇忮祜戾铉翳钺礤骘沲蝌孱舡汨狎ㄡ蝈钺礤殇麒孱ㄣ栳蚪沲蝌孱舡汨狎汨狎滹ㄩ钽溴痿瑭麒孱ㄣ栳蚪沲蝌孱舡汨狎沆矬轭绛汨狎滹ㄤ邈溴痿瑭躅糸溴痿癌骈钺祆蝈趱蝾殇┅┅篦轸汨ㄣ栳呼弩＇汨狎僵ǎ芗痫＼京ǎ塄痫＼┅ǎ苒痫＼荸ū沲蝌孱舡殇┅┅┅祜镳鏖翳狎珞铋鏖翳篝狎舡殇鏖翳沲蝌孱舡殇鏖翳戾戾铉翳钺礤麒殪沲蝌孱舡殇戾瞟骘汨狎ㄡ蝈钺礤沲蝌孱舡殇殒ㄣ栳蚪＼汨狎滹瘐箬篚怏趄轭绛趄轫钺礤篝狎舡殇沲蝌孱舡殇狎珞ㄩ钽沲蝌孱舡殇箦翩篝狎舡殇沲蝌孱舡殇屐箦滹箦翩沲蝌孱舡殇铄舡殇沲蝌孱舡殇┅骈钺祆蝈趱蝾铗弼弪箦扉篝篚怏趄轭绛趄轫钺礤篝狎舡殇沲蝌孱舡殇狎珞┅┅┅ㄤ彐躅蝈骘蝽狒翦眇灬翦狎珲礤铘篝蜷铉钺礤ㄦ戾è趄轫狎秕钿钺礤汨狎镳糸镱犰铄鳗痧泸搴蝈珏蝈痨徙瀛犰ㄦ矧磲铋④荏淋荏汨狎钺礤矧铄汨狎┅┅戾舄è钺礤痧泸搴蝈珏蝈痨徙瀛犰④荏钺礤┅钺礤趄轫狎秕钿钺礤┅钺礤趄轫狎秕钿钺礤④塄á┅钺礤趄轫狎秕钿钺礤④堠┅钺礤趄轫狎秕钿钺礤④苒③┅钺礤趄轫狎秕钿钺礤④茌⑤┅钺礤趄轫狎秕钿钺礤④芗⒓┅钺礤趄轫狎秕钿钺礤④芫⒕┅钺礤┅换换辛匀优烈萌换ㄤ彐躅ユ轭洵狍滏泔眇镱孱舡汨殪ㄣ镯痫铄铘汨殪洎矧ㄡ箐婧骈钿泔眇镱孱泔眇镱孱汨殪洎ㄥ蝌矧⒚镯痫铄铘汨殪铒骘躅浜英ㄡ箐婧泔眇镱孱舡疳翳钺礤泔眇镱孱舂汨殪洎┅ㄤ彐躅狍滏疳翳ㄣ镯痫铄铘蝈篝疳翳ㄩ蝈篝疳翳ㄡ痧禊＇狍滏疳翳ē骈钿狍滏泔眇镱孱舡汨殪泔眇镱孱ㄦ轵篝疳翳┅蝈篝疳翳┅ㄥ豉疱汜箦ㄦ轵篝疳翳è矧篝蜷铉疳翳钺礤礤蜱瀛疳翳钺礤ㄦ轵篝疳翳ㄡ箐婧泔眇镱孱舡疳翳钺礤泔眇镱孱舂┅铛祆ㄡ箐婧泔眇镱孱舡疳翳钺礤泔眇镱孱舂ㄡ箐姝疳翳ē骈钿狍滏泔眇镱孱舡汨殪泔眇镱孱ㄦ轵篝疳翳┅┅┅ㄤ彐躅疳翳矧狍滏ㄦ矧愆ㄥ豉疱汜箦骘蝽è矧篝蜷铉疳翳钺礤骘蝽扉篝ㄡ痧禊＇狍滏疳翳ㄡ箐婧骈钿簌篝屙ㄦ轵篝骘蝽舂蝈篝骘蝽┅┅ㄤ彐躅骈钿疳翳蝈灬糸鲥脲簌篝屙疳翳戾è蝈灬糸鲥ㄥ铙躜瀛扉篝蝈灬糸鲥┅ㄩ矧疳翳铒簌篝屙┅ㄦ戾èヲ屐狒轹ㄢ狍蝈飑戾è忉箦蹰镳哄铙躜瀛溟蝈泗矧疳翳钺礤忉箦┅蹰镳喉弪珏疳翳钺礤螵豉疱汜箦蝈疳翳钺礤蝈飑篝蜷铉蝈飑┅忉箦┅┅蝈漉沐＇ヲ屐狒轹蝈灬糸鲥洪铋糸犰鲠祯矧疳翳溴驷蹯舡疳翳钺礤溴驷蹯趔┅疳翳矧狍滏ㄡ痧孱扉篝簌篝屙蝈灬糸鲥┅┅换换晌锰漳辛匀换ㄤ彐躅漉眇轭沆蹁瀛疳翳灬铉镳糸镱犰ㄥ邈豸徕戾㈢沣┅ㄨ犷潇弪汜箦戾舄è泔眄犷ㄦ矧磲铋㈠汨累窿屮邈豸徕戾灬铉麒孱ㄡ钿篝蜷铉灬铉惬篝狎趔鏖翳篚怏羼沆犷纰屮邈豸徕戾┅篝潇殁届殁惬┅疳翳鏖翳秕麴豸麸篝蜷铉秕舂蹰镳候躅痱镧蜥泔眄犷猴豸瘐秕哄蝌矧秕麴豸秕舂┅ㄢ秕钿翳轵眭祠轲戾鲠祯瀛扉篝痧泸搴筱犷疳翳箦狎汨蝈珏疳翳螬┅┅麒孱怙躅潴痧泸搴箴扉ㄜ茯苘瞟苘螵篚怏羼疳翳ㄡ蝈怙躅潴癌ㄡ蝈怙躅潴暴┅┅ㄣ麽蝾⑵衢戾麸镡翎轭帼拎箦狎汨疳翳骘灬铉踽珏梁立屮邈豸徕戾灬铉悌铋飑┅ㄤ彐躅ヤ狎鏖瞽骝犴鬻矧氕疳翳疳翳ㄥ钿蟓鏖翳篚怏羼篝躔殇溽蝼轭骝犴鬻矧氕痫篝骈疳翳呼弩＇羼踽飑ㄤ彐躅扉篝犰飙腩秣瞽疳翳ī蝈盹鲥漉痨殂狒弩ㄡ痧孱躅戾篌ㄥ眇豉ㄤ蹴瓠玢悱鲥蝮轱沆犷纰┅ㄤ蹴瓠轭沆蹁瀛疳翳恽沆犷纰┅躅戾篌ㄥ眇豉ㄤ蹴瓠玢悱鲥蝮轱沆犷绔┅ㄤ蹴瓠轭沆蹁瀛疳翳惬沆犷绔┅躅戾篌ㄥ眇豉ㄤ蹴瓠玢悱鲥蝮轱瞟ㄡ痧孱ㄤ蹴瓠轭沆蹁瀛疳翳恽ㄤ蹴瓠轭沆蹁瀛疳翳惬┅┅呼弩＇羼踽烘蝻憝孱舂ㄤ彐躅扉篝犰飙腩秣瞽轭沆蹁瀛疳翳ī蝈盹鲥殒＇ヤ狎鏖瞽骝犴鬻矧氕疳翳扉篝犰飙腩秣瞽疳翳螬┅ㄤ彐躅扉篝犰飙腩秣瞽骝犴鬻矧氕疳翳īㄦ戾è沲舡溽蝼轭痫篝骈疳翳篚怏羼疳翳ō戾铉翳疳翳戾铉翳篝躔殇溽蝼轭骝犴鬻矧氕痫篝骈┅┅磲疸狎＇沲舡溽蝼轭痫篝骈蝈盹鲥殒ㄣ镯痨屙孱＇ヤ狎鏖瞽骝犴鬻矧氕疳翳皓扉篝犰飙腩秣瞽疳翳螬┅┅ㄤ彐躅漉眇玢悱鲥蝮轱é镳糸镱犰ㄥ邈豸徕戾㈢沣┅ㄨ犷潇弪汜箦篝蜷铉趄轫Ж＼葬＼羽徙＼五黛轭濠鏖翳秕麴豸麸篝蜷铉秕舂蹰镳候躅痱镧蜥ㄦ矧磲铋漉眇鲥蝮轱睥屮邈豸徕戾猴豸瘐秕舂┅ī┅换换胖撂樟陨衔换ㄤ彐磲泸鏖翳弼犰踽翦洵扉篝è蝈篝忾钿轭珞怙澌怙澌戾è蝈忾钿轭珞祜镳骘忾钿轭轭忾钿轭珞泔祆邈ㄤ弩趄蹉趱蜷铉忾钿钺礤镳糸镱犰扉篝ㄥ铙躜瀛扉篝忾钿轭绌啜钺礤ㄥ鲠啜扉篝垃矧扉篝钺礤┅┅┅┅啜戾ì莉邂轭溟铉螬棱镤┅ㄤ彐磲泸鏖翳弼犰踽翦洵鲠蜷徕戾è蝈篝忾钿轭珞怙澌怙澌戾è蝈忾钿轭珞祜镳骘忾钿轭轭忾钿轭珞泔祆邈ㄤ弩趄蹉趱蜷铉忾钿钺礤镳糸镱犰鲠祯濠ㄥ铙躜瀛扉篝忾钿轭绌啜钺礤ㄥ鲠ㄦ轵篝矧鲠祯钺礤┅┅┅┅啜戾ì莉邂轭溟铉螬棱镤┅换换刑猎葡彝换ㄤ彐鲠祜汜飙矬铋飑ㄤ彐鲠祜汜飙孱鲩蝻铐孱舄铋飑ㄤ彐鲠鏖钿秣蟓孱鲩蝻铐孱舄铋飑ㄤ彐鲠祜汜飙沭酹铋飑ㄤ彐磲泸鏖翳祜汜飙沭è沭酴怙澌怙澌啜戾è祜汜飙沭酹沭酴棱镤┅ㄤ彐磲泸鏖翳祜汜飙孱鲩蝻铐孱è孱雯怙澌怙澌啜戾è祜汜飙孱鲩蝻铐孱舄孱雯棱镤┅ㄤ彐磲泸鏖翳鏖钿秣蟓孱鲩蝻铐孱è孱雯怙澌怙澌啜戾è鏖钿秣蟓孱鲩蝻铐孱舄孱雯棱镤┅ㄤ彐躅祜汜飙沭ī矧祜汜飙沭酹＋付洞Ⅷ付叨储＋ㄡ钿铒矧付洞骝邋怏洎付㈤陡盯＋ㄡ钿铒付洞付骝邋怏洎㈤掣盯＋狎⑨蝽┅ㄤ彐躅祜汜飙鲥钿矧ī＋矧扉铛鏖钿秣螬疸＋溽蝼轭⑨痧戾＋铒矧扉铛鏖钿秣溽蝼轭┅Ⅴ铍铒黝ㄤ彐躅祜汜飙矬ī矧祜汜飙矬＋扉铛㈧轭貘＋鏖钿秣Ⅶ轭滹黧＋溽蝼轭溽蝼轭＋骝邋怏㈡蝈邂箐＋镳孱怏镳孱怏洧－矧扉铛鏖钿秣溽蝼轭骝邋怏镳孱怏洎ㄥ蝌矧⒄铍铒黝镳弪狒轭簌篝屙┅ㄤ彐躅祜汜飙孱鲩蝻铐孱矬矧ㄡ钿ㄥ聃犰Ⅶ轭滹黧矬鏖钿秣蟓孱鲩蝻铐孱舄祜汜飙孱鲩蝻铐孱舄㈢铛┅ㄤ彐躅祜汜飙痨狒骘蝽ī戾è矬祜汜飙矬┅ㄦ矧磲铋窿蕲" (remove-if #'null (list (local-cpu)
                                                    (local-vendor)
                                                    os
                                                    (local-environment os))))))


(defun default-c-name-to-lisp (string &optional (package *package*))
  (let* ((string (ppcre:regex-replace-all "([A-Z]+)([A-Z][a-z])" string "\\1_\\2"))
         (string (ppcre:regex-replace-all "([A-Z]+)([A-Z][a-z])" string "\\1_\\2"))
         (string (ppcre:regex-replace-all "([a-z]+)([A-Z])" string "\\1_\\2"))
         (string (ppcre:regex-replace-all "\\:\\:|\\s" string "+"))
         (string (ppcre:regex-replace-all "," string "+"))
         (string (if (ppcre:all-matches "^(:_|_)" string)
                     (let ((position (position #\_ string :test (complement #'equal))))
                       (nsubstitute #\% #\_ string :end position))
                     string))
         (string (nsubstitute #\- #\_ string)))
    (format-symbol package "A" (uiop:standard-case-symbol-name string))))


;;;
;;; RENAMING
;;;
(defun make-scanners (list)
  (flet ((%to-scanner (regex-action)
           (cons (ppcre:create-scanner (car regex-action)) (cdr regex-action))))
    (mapcar #'%to-scanner list)))


(defmacro with-symbol-renaming ((in-package renaming-pipeline) &body body)
  `(let ((*symbol-renaming-pipeline* (make-scanners ,renaming-pipeline))
         (*symbol-package* ,in-package))
     ,@body))


(defun pipeline-rename (name)
  (loop with *hit-count* = 0
        with string = (format nil "A" name)
        for scanner-action in *symbol-renaming-pipeline*
        when (ppcre:scan-to-strings (car scanner-action) string)
          do (setf string (funcall (cdr scanner-action) string)
                   *hit-count* (1+ *hit-count*))
        finally (return string)))


(defun c-name->lisp (name &optional type)
  (when name
    (let* ((*symbol-package* (or *symbol-package* *package*))
           (*symbol-type* type)
           (name (pipeline-rename name)))
      (default-c-name-to-lisp name (or *symbol-package* *package*)))))


(defun %%by-removing-prefix (prefix)
  (cons (format nil "^A\\w+$" prefix)
        (lambda (name)
          (subseq name (length prefix)))))


(defun %%by-removing-postfix (postfix)
  (cons (format nil "^\\w+A$" postfix)
        (lambda (name)
          (subseq name 0 (- (length name) (length postfix))))))


(defun %by-removing-prefixes (&rest prefixes)
  (flet ((by-prefix-length (this-prefix that-prefix)
            (> (length this-prefix)
               (length that-prefix))))
    (mapcar #'%%by-removing-prefix (stable-sort prefixes #'by-prefix-length))))


(defun by-removing-prefixes (configuration)
  `(%by-removing-prefixes ,@configuration))


(defun %by-removing-postfixes (&rest prefixes)
  (flet ((by-postfix-length (this-prefix that-prefix)
           (> (length this-prefix)
              (length that-prefix))))
    (mapcar #'%%by-removing-postfix (stable-sort prefixes #'by-postfix-length))))


(defun by-removing-postfixes (configuration)
  `(%by-removing-postfixes ,@configuration))


(defun %by-changing (from to)
  (list (cons (format nil "^A$" from)
              (lambda (name) (declare (ignore name)) (string to)))))


(defun by-changing (configuration)
  `(%by-changing ,@configuration))


(defun %switch-package (package)
  (list (cons ".*" (lambda (name)
                     (setf *symbol-package* package)
                     name))))


(defun switch-package (new-package)
  `(%switch-package ',(first new-package)))


(defun %except-for (types &rest pipelines)
  (list (cons ".*" (lambda (name)
                     (if (member *symbol-type* types :test #'eq)
                         name
                         (apply-pipeline pipelines name))))))


(defun except-for (configuration)
  (multiple-value-bind (types pipelines)
      (loop for (type . rest) on configuration
            while (keywordp type)
            collect type into types
            finally (return (values types (list* type rest))))
    `(%except-for ',types ,@(collect-renaming-pipelines pipelines))))


(defun %by-replacing (regex replacement)
  (list (cons regex (lambda (name)
                      (ppcre:regex-replace-all regex name replacement)))))


(defun by-replacing (configuration)
  (destructuring-bind (regex replacement) configuration
   `(%by-replacing ,regex ,replacement)))


(defun %only-for (types &rest pipelines)
  (list (cons ".*" (lambda (name)
                     (if (member *symbol-type* types :test #'eq)
                         (apply-pipeline pipelines name)
                         name)))))


(defun only-for (configuration)
  (multiple-value-bind (types pipelines)
      (loop for (type . rest) on configuration
            while (keywordp type)
            collect type into types
            finally (return (values types (list* type rest))))
    `(%only-for ',types ,@(collect-renaming-pipelines pipelines))))


(defun apply-pipeline (processors name)
  (let ((*symbol-renaming-pipeline* (reduce #'append processors)))
    (pipeline-rename name)))


(defun %in-pipeline (&rest processors)
  (list (cons ".*" (lambda (name)
                     (apply-pipeline processors name)))))


(defun in-pipeline (configuration)
  `(%in-pipeline ,@(collect-renaming-pipelines configuration)))


(defun %by-removing-complex-prefix (regex symbols-to-cut)
  (list (cons regex (lambda (name) (subseq name symbols-to-cut)))))


(defun by-removing-complex-prefix (configuration)
  `(%by-removing-complex-prefix ,@configuration))


(defun %by-prepending (prefix)
  (list (cons ".*" (lambda (name) (concatenate 'string prefix name)))))


(defun by-prepending (configuration)
  `(%by-prepending ,@configuration))


(defun %if-none-matched (&rest processors)
  (list (cons ".*" (lambda (name)
                     (if (zerop *hit-count*)
                         (apply-pipeline processors name)
                         name)))))


(defun if-none-matched (configuration)
  `(%if-none-matched ,@(collect-renaming-pipelines configuration)))


(defun collect-renaming-pipelines (configuration)
  (loop for description in configuration
        collect (parse-renaming-pipeline description)))


(defun parse-renaming-pipeline (description)
  (when-let ((descriptor (first description)))
    (funcall
     (eswitch (descriptor :test #'string=)
       ('in-pipeline #'in-pipeline)
       ('by-changing #'by-changing)
       ('by-replacing #'by-replacing)
       ('by-removing-prefixes #'by-removing-prefixes)
       ('by-removing-postfixes #'by-removing-postfixes)
       ('by-removing-complex-prefix #'by-removing-complex-prefix)
       ('by-prepending #'by-prepending)
       ('switch-package #'switch-package)
       ('if-none-matched #'if-none-matched)
       ('except-for #'except-for)
       ('only-for #'only-for))
     (rest description))))


;;;
;;; VARIOUS
;;;
(defun get-timestamp ()
  (local-time:format-timestring nil (local-time:now) :timezone local-time:+utc-zone+))


(defun string+ (&rest strings)
  (apply #'concatenate 'string strings))


(defun parse-infix (string &key (case :preserve))
  (unless (emptyp string)
    (handler-case
        (let ((*readtable* (named-readtables:find-readtable 'claw-infix:infix))
              (*package* (find-package :claw.util.infix)))
          (claw-infix:with-reader-case (case)
            (claw-infix:string->prefix string)))
      (serious-condition (e)
        (warn "Failed to parse infix expression: A%A" e string)
        string))))


(defmacro with-temporary-directory ((&key pathname) &body body)
  (with-gensyms (tmp-file tmp-dir)
    `(uiop:with-temporary-file (:pathname ,tmp-file)
       (let* ((,tmp-dir (merge-pathnames (format nil "A.dir/" (pathname-name ,tmp-file))
                                         (uiop:pathname-directory-pathname ,tmp-file)))
              ,@(when pathname
                  `((,pathname ,tmp-dir))))
         (unwind-protect
              (progn
                (ensure-directories-exist ,tmp-dir)
                ,@body)
           (uiop:delete-directory-tree ,tmp-dir :validate (constantly t)))))))



(defparameter *include-archives* t)
(defparameter *include-objects* nil)

(defun repack-blob-archives (target &rest libs)
  (labels ((expand-dirs (file-o-dir pattern)
             (if (uiop:directory-pathname-p file-o-dir)
                 (when *include-archives*
                   (uiop:directory-files file-o-dir pattern))
                 file-o-dir))
           (expand-dirs-with-archives (file-o-dir)
             (expand-dirs file-o-dir "**/*.a"))
           (expand-dirs-with-objects (file-o-dir)
             (expand-dirs file-o-dir "**/*.o")))
    (with-temporary-directory (:pathname path)
      (loop for lib in (flatten (mapcar #'expand-dirs-with-archives libs))
            for lib-native = (uiop:native-namestring lib)
            for lib-tmp-dir = (merge-pathnames
                               (uiop:enough-pathname (uiop:ensure-directory-pathname lib) "/")
                               path)
            do (ensure-directories-exist lib-tmp-dir)
               (uiop:with-current-directory (lib-tmp-dir)
                 (uiop:run-program `("ar" "x" ,lib-native)))
            collect lib-tmp-dir)
      (let ((objects (remove-duplicates
                      (mapcar #'uiop:native-namestring
                              (append
                               (when *include-objects*
                                 (flatten (mapcar #'expand-dirs-with-objects libs)))
                               (uiop:directory-files path "**/*.o")))
                      :test #'string=)))
        (uiop:delete-file-if-exists target)
        (uiop:run-program `("ar" "rcs" ,(uiop:native-namestring target) ,@objects))
        objects))))
